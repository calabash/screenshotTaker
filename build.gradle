plugins {
    id 'java'
    id 'application'
}

mainClassName = 'sh.calaba.screenshot.ScreenshotTaker'

sourceSets {
    intTest {
        java.srcDir file('src/intTest/java')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

dependencies {
    compile fileTree(dir: 'lib', include: '*.jar')
    testCompile 'junit:junit:4.12'
}

repositories {
    jcenter()
}

jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }

    manifest {
        attributes 'Main-Class': 'sh.calaba.screenshot.ScreenshotTaker'
    }

    baseName = 'screenshotTaker'
}

task integrationTest(type: JavaExec) {
    def imageFromAppFileName = 'screenshotTakerTest.png'
    def imageFromAdbFileName = 'adbScreenshotTest.png'

    doFirst {
        def androidHome = System.getenv('ANDROID_HOME')
        if (androidHome == null) {
            throw new GradleException('ANDROID_HOME environment variable must be set to proper Android SDK location before executiong this test')
        }

        def adb = new File(androidHome, 'platform-tools/adb')
        def jar = new File(rootDir, 'screenshotTaker.jar')

        String deviceSerial = null;

        def getSerailResult = exec {
            standardOutput = new ByteArrayOutputStream();
            ignoreExitValue = true

            commandLine adb, 'get-serialno'

            ext.output = {
                return standardOutput.toString()
            }
        }

        if (getSerailResult.getExitValue() == 0) {
            deviceSerial = ext.output().trim()
        } else {
            deviceSerial = System.getenv('ADB_SERIAL')
            if (deviceSerial == null) {
                System.out.println(ext.output())

                exec {
                    commandLine adb, 'devices'
                }

                def message = 'Please set ADB_SERIAL environment variable to identify the device: $ ADB_SERIAL=<serial number> gradlew integrationTest'
                System.out.println(message)
                throw new GradleException()
            }
        }

        javaexec {
            main = "-jar"
            args = [jar, deviceSerial, new File(rootDir, imageFromAppFileName)]
        }

        def pathOnDevice = "/sdcard/$imageFromAdbFileName"
        exec {
            commandLine adb, '-s', deviceSerial, 'shell', 'screencap', pathOnDevice
        }
        exec {
            commandLine adb, '-s', deviceSerial, 'pull', pathOnDevice, new File(rootDir, imageFromAdbFileName)
        }
    }

    description = 'Runs integration tests.'
    group = 'verification'

    main = 'src.ScreenshotTakerTest'
    classpath = sourceSets.intTest.runtimeClasspath
    args = [imageFromAppFileName, imageFromAdbFileName]

    dependsOn build
}

tasks.withType(Jar) {
    destinationDir = rootDir
}